// Implements the classic "Animals" guessing game. The user thinks of an animal.
// The program asks a series of yes/no questions to try to guess the animal
// they are thinking of. If the program fails, it asks the user for a new
// question and adds the animal to its knowledge base.
//
// Internally, the program's brain is stored as a binary tree. Leaf nodes are
// animals. Internal nodes are yes/no questions that choose which branch to
// explore.

class Node

  // Writes a prompt and reads a string of input.
  func promptString(prompt)
    print(prompt, terminator: '')
    return readln()
  end

  // Reads a "yes" or "no" (or something approximating) those and returns true
  // if yes was entered.
  func promptYesNo(prompt)
    while True do
      var line := self.promptString(prompt)

      if (line[0] = "y) | line[0] = "Y) then
        return True
      end
      if (line[0] = "n) | line[0] = "N) then
        return False
      end

      // Quit.
      break on (line[0] = "q) | (line[0] = "Q)
    end
    return Null
  end

end

class Question (Node)
  var question := ''
  var ifYes := Null
  var ifNo := Null

  init (question, ifYes, ifNo)
    self.question := question
    self.ifYes := ifYes
    self.ifNo := ifNo
  end

  func ask()
    // Recurse into the branches.
    if self.promptYesNo(self.question) then
      var result := self.ifYes.ask()
      if result <> Null then self.ifYes := result end
    else
      var result := self.ifNo.ask()
      if result <> Null then self.ifNo := result end
    end
  end
end

class Animal (Node)
  var name := ''
  init (name)
    self.name = name
  end

  func ask()
    // Hit a leaf, so see if we guessed it.
    if self.promptYesNo('Is it a ' + self.name + '?') then
      print('I won! Let us play again!')
      return Null
    end

    // Nope, so add a new animal and turn this node into a branch.
    var name := self.promptString('I lost! What was your animal?')
    var question := self.promptString('What question would distinguish a ' +
      self.name + ' from a ' + name + '?')
    var isYes := self.promptYesNo(
        'Is the answer to the question "yes" for a ' + name + '?')
    print('I will remember that. Let us play again!')

    var animal := Animal(name)
    return Question(question,
      if isYes then animal else self,
      if isYes then self else animal)
  end
end


var root := Question('Does it live in the water?',
    Animal('frog'), Animal('goat'))

// Play games until the user quits.

while True do
  root.ask()
end

